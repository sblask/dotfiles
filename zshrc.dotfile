# autostart tmux
if [ "$TMUX" = "" ] && [ "$(hostname)" = "arraia" ];
    then exec tmux;
fi

# enable vi mode
bindkey -v
# enable color names instead of codes
autoload -U colors
colors

#
# prompt helper functions
#
function git_branch {
  git branch --no-color 2> /dev/null | sed -e '/^[^*]/d' -e 's/* \(.*\)/\1 /'
}
function git_no_mail_warning {
  git status 1>/dev/null 2>/dev/null
  if [ $? -ne 128 -a $? -ne 127 -a "$(git config user.email)" = "" -a "$EMAIL" = "" ]; then
    echo -n '(user.email not configured) '
  fi
}
function my_ip {
  IP_LINE="$(ip addr show dev eth0 | grep 'inet ')"
  if [ "$IP_LINE" = "" ]; then
    IP_LINE="$(ip addr show dev wlan0 | grep 'inet ')"
  fi
  echo -n $IP_LINE | awk '{split($0,a,"(/| +)"); print a[3],""}'
}
function virtual_env {
  if [ "$VIRTUAL_ENV" = "" ]; then
      echo ""
  else
      echo "($(basename $VIRTUAL_ENV)) "
  fi
}

#
# set up the prompt
#
function insert-mode { echo "-- INSERT --" }
function normal-mode { echo "-- NORMAL --" }
function preexec     { print -rn -- $terminfo[el]; }
function set-prompt  {
    PS1=""
    PS1=$PS1"%{$terminfo[cud1]$terminfo[cuu1]$terminfo[sc]$terminfo[cud1]$1$terminfo[rc]%}"
    PS1=$PS1"%~ $ "
}
# just print the first two lines to avoid the problem with multi-line and reset-prompt
function precmd {
    FIRST_LINE="\n"
    FIRST_LINE=$FIRST_LINE"[%D{%a, %d %b %Y, %H:%M:%S}] "
    FIRST_LINE=$FIRST_LINE"%{$fg[blue]%}$(virtual_env)%{$reset_color%}"
    FIRST_LINE=$FIRST_LINE"%n "
    FIRST_LINE=$FIRST_LINE"%{$fg[blue]%}%m%{$reset_color%} "
    FIRST_LINE=$FIRST_LINE"$(my_ip)"
    FIRST_LINE=$FIRST_LINE"%{$fg[blue]%}$(git_branch)%{$reset_color%}"
    FIRST_LINE=$FIRST_LINE"%{$fg[red]%}$(git_no_mail_warning)%{$reset_color%}"

    print -P "$FIRST_LINE"
    # initial prompt and fix for problem with Ctrl+C and Enter
    set-prompt "$(insert-mode)"
}
function zle-line-init zle-keymap-select {
    # for cursor shapes, see http://stackoverflow.com/questions/4416909/anyway-change-the-cursor-vertical-line-instead-of-a-box
    if [ "${KEYMAP}" = "vicmd" ]; then
        echo -e -n "\x1b[\x31 q" # changes to blinking block
        set-prompt "$(normal-mode)"
    else
        echo -e -n "\x1b[\x35 q" # changes to blinking bar
        set-prompt "$(insert-mode)"
    fi
    zle reset-prompt
}
zle -N zle-line-init
zle -N zle-keymap-select

#
# Setup completions
#
# enable more completions
fpath=(~/.zsh/completions.d $fpath)
autoload -U compinit
compinit
# make completion work with aliases even if that breaks hub
setopt COMPLETE_ALIASES
# complete in both directions
setopt COMPLETE_IN_WORD
# enable # ~ ^ in globs
setopt EXTENDED_GLOB
# general options
zstyle ':completion:*' insert-tab pending # pasting with tabs doesn't perform completion
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}' # complete case insensitive
zstyle ':completion:*' menu select # highlight current selection and make suggestions navigable

#
# Various Completions
#
zstyle ':completion:*:kill:*' command 'ps f -u $USER -o pid,%cpu,%mem,cmd'
zstyle ':completion:*:kill:*' force-list always
zstyle ':completion:*:kill:*' menu yes select
zstyle ':completion:*:kill:*:processes' list-colors '=(#b) #([0-9]#)*=0=01;31'
zstyle ':completion:*:kill:*:processes' sort false
# make sure we do not get the same suggestion twice
zstyle ':completion:*:diff:*' ignore-line yes
zstyle ':completion:*:kill:*' ignore-line yes
zstyle ':completion:*:rm:*' ignore-line yes



#
# Setup history
#
HISTSIZE=1000000
SAVEHIST=1000000
HISTFILE=~/.zsh_history
setopt EXTENDED_HISTORY
setopt HIST_IGNORE_DUPS
setopt HIST_REDUCE_BLANKS
setopt INC_APPEND_HISTORY

#
# Keybindings
#
bindkey -M viins '^P' expand-or-complete-prefix
# make backspace work after returning from command mode
bindkey -M viins '^?' backward-delete-char
# make Ctrl+Arrow work
bindkey -M viins '^[[1;5C' forward-word
bindkey -M viins '^[[1;5D' backward-word
bindkey -M vicmd '^[[1;5C' forward-word
bindkey -M vicmd '^[[1;5D' backward-word
# make home and end work
bindkey -M viins '^[[H' beginning-of-line
bindkey -M viins '^[[F' end-of-line
bindkey -M vicmd '^[[H' beginning-of-line
bindkey -M vicmd '^[[F' end-of-line
# vim type incremental search
bindkey -M viins '^R' history-incremental-search-backward
bindkey -M vicmd '^R' history-incremental-search-backward
bindkey -M vicmd '/' history-incremental-search-backward
bindkey -M vicmd '?' history-incremental-search-forward
# allow going back in selection menu using Shift-Tab
bindkey -M viins '^[[Z' reverse-menu-complete
# undo last completion/change with Ctrl+Z and u
bindkey -M viins '^Z' undo
bindkey -M vicmd '^Z' undo
bindkey -M vicmd 'u' undo
# redo with U
bindkey -M vicmd 'U' redo
# clear prompt, but return after next command
bindkey -M vicmd 'q' push-line
# edit current line in editor
autoload -z edit-command-line
zle -N edit-command-line
bindkey -M viins "^E" edit-command-line
bindkey -M vicmd "^E" edit-command-line

#
# Various settings
#
export KEYTIMEOUT=1
# enable using comments in command line
setopt INTERACTIVE_COMMENTS
# propose action if command was not found
source /etc/zsh_command_not_found
# no need for cd
setopt AUTO_CD
# automatically push directories to the stack, show stack with dirs -v, change to one using ~index
setopt AUTO_PUSHD
# spell check commands
setopt CORRECT
# disable Ctrl-S and Ctrl-Q
setopt NO_FLOW_CONTROL

#
# External files
#

# load scm breeze here to allow overwriting aliases
if [ -f ~/.scm_breeze/scm_breeze.sh ]; then
    . ~/.scm_breeze/scm_breeze.sh
fi
# aliases
if [ -f ~/.aliases ]; then
    . ~/.aliases
fi
# load settings that are local to a specific machine
if [ -f ~/.zsh_local ]; then
    . ~/.zsh_local
fi
# use virtualenvwrapper if available
if [ -f /usr/share/virtualenvwrapper/virtualenvwrapper.sh ]; then
    . /usr/share/virtualenvwrapper/virtualenvwrapper.sh
fi

#
# Exports
#
export EDITOR=vim
export LS_COLORS='di=34:ln=33:or=31:ex=32'
export MANPAGER="/bin/sh -c \"col -b | vim -c 'set filetype=man tabstop=8 nomodified nomodifiable nolist nonumber' -\""
export PAGER='less -K'
export PATH=~/.bin:$PATH
export PIP_DOWNLOAD_CACHE=~/.pip_download_cache
export VIRTUAL_ENV_DISABLE_PROMPT=True

#
# Suffix Aliases
#

# install .deb files including their dependencies
alias -s deb="sudo gdebi"
# run python files
alias -s py="python"
#
alias -s txt="vim"
# use the same association as XFCE
alias -s avi="xdg-open"
alias -s flac="xdg-open"
alias -s gif="xdg-open"
alias -s jpeg="xdg-open"
alias -s jpg="xdg-open"
alias -s mpeg="xdg-open"
alias -s mpg="xdg-open"
alias -s mp3="xdg-open"
alias -s pdf="xdg-open"
alias -s png="xdg-open"
alias -s wmv="xdg-open"

#
# Global Aliases
#
alias -g H="| head"
alias -g T="| tail"
alias -g C="| wc -l"
alias -g L="| less"
alias -g G="| grep"
alias -g S="| sed -e"
alias -g A="| awk"

#
# Other Aliases
#

alias vimpager="/bin/sh -c \"col -b | vim -c 'set filetype=man tabstop=8 nomodified nomodifiable nolist' -\""
# misc
alias cal='echo -n "       Week "; week;ncal -wC'
alias cd..='cd ..'
alias fix_locales='sudo locale-gen en_GB.UTF-8 && sudo dpkg-reconfigure locales'
alias jsonp='python -m json.tool'
alias less='less -K'
alias psgrep='ps aufx | grep'
alias psless='ps aufx | vimpager'
alias pss='ps aufx'
alias sbte='./sbt "eclipse with-source=true"'
alias top='top -c'
alias untar='tar -xzf'
alias week='date +%W'
# apt-get
alias autoremove='sudo apt-get autoremove --purge'
alias install='sudo apt-get install'
alias policy='apt-cache policy'
alias uninstall='sudo apt-get purge'
# virtualenv stuff
alias mkenv='mkvirtualenv'
alias on='workon'
alias off="deactivate"
# top
alias topcpu='top -b -n1 | tail -n +8 | sort -n -r -k 9'
alias tophead='top -b -n1 | head -n 5'
alias topmem='top -b -n1 | tail -n +8 | sort -n -r -k 10'
# git
alias gdc='gd --cached'
alias gdtc='gdt --cached'
GIT_LOG_FORMAT='%C(blue)%h%Creset -%C(yellow)%d%Creset %s %C(blue)(%ai) %C(bold blue)<%an (%ae)>%Creset'
alias gl='git log --graph --decorate --pretty=format:$GIT_LOG_FORMAT --abbrev-commit --date=short'
alias glh='gl | head'
alias gla='gl --all'
alias glah='gla | head'
alias gld='gl --left-right --graph --cherry-mark'
alias gldo='gl --left-right --graph'
alias gmm='git merge --ff-only origin/master'

#
# Functions
#
function mcd() {
  mkdir $1 && cd $1
}

#
# Plugins
#
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets)
source ~/.zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
